Assume  CS: Code, DS: Code
Code 	SEGMENT
	org 	100h
	.286
Start 	proc 	near 	; Основная процедура
	mov 	ax,cs
	mov 	ds,ax 	; DS = CS
	jmp 	beg 	; "Перескок" через переменные
iniflag db 	0 	; Флаг звучания
old_int1c_off 	dw 	0 	; Смещение старого вектора
old_int1c_seg 	dw 	0 	; Сегмент старого вектора
tek_mel 		dw 	? 	; Адрес текущей ноты выбранной мелодии
beg: 	mov 	ax,351ch 	; Сохранение старого вектора 1c
	int 	21h
	mov 	cs:old_int1c_off,bx 	; Запись смещения
	mov 	cs:old_int1c_seg,es 	; Запись сегмента
	lea 	dx,new_int1c 		; Запись нового вектора 1c
	mov 	ah,25h 		; Функция установки вектора прерыв.
	mov 	al,1ch			; Номер вектора прерывания
	int 	21h 			; DS:DX - адрес новой программы обр.
beg1: call 	kbin 	; Опрос клавиатуры
	cmp 	al,'1'	; = '1' ?
	jnz 	beg2 	; Нет
	mov 	byte ptr iniflag,1 	; Взведение флага звуч.
	lea 	ax,mel1
	mov 	tek_mel,ax
	jmp 	beg1 		; Переход на начало цикла
beg2: cmp 	al,'2'	 ; = '2' ?
	jnz	beg3 	; Нет
	mov 	byte ptr iniflag,1 	; Сброс флага звуч.
	lea 	ax,mel2
	mov 	tek_mel,ax
	jmp 	beg1 		; Переход на начало цикла
beg3: cmp 	al,'3' 	; = '3' ?
	jnz 	beg4 	; Нет
	mov 	byte ptr iniflag,1 	; Сброс флага звуч.
	lea 	ax,mel3
	mov 	tek_mel,ax
	jmp 	beg1 		; Переход на начало цикла
beg4: cmp 	al,'q' 	; = 'q' ?
	jnz	beg1 	; Нет
; Восстановление старого вектора 1с и выход
	mov 	dx,old_int1c_off 	; Смещение старого вектора
	mov 	ax,old_int1c_seg 	; Сегмент старого вектора
	mov 	ds,ax 			; DS:DX - адрес устанавл. вектора
	mov 	ax,251ch 		; Установка старого вектора 1ch
	int 	21h
	int 	20h
start 	endp
; Новый обработчик прерывания 1ch
new_int1c 	proc 	far 	; Дальняя процедура
		pusha 	; Сохранение всех регистров
		call 	muz 	; Вызов процедура извлечения звука
		popa 		; Восстановление всех регистров
		iret 		; Возврат из программы обработки прерывания
new_int1c 	endp
; Процедура извлечения очередного звука
; tek_mel - адрес текущей ноты выбранной мелодии
muz 	proc 	near
	test 	byte ptr cs:iniflag,0ffh 	; Проверка флага
	jnz 	muz1 			; Продолжение
muze: in 	al,61h 	; Чтение состояния системного порта В
	and 	al,0fch 	; Запрещение звучания (биты 0 и 1)
	out 	61h,al 	; Запись в системный порт В
	ret 			; Выход, если флаг не взведен
muz1: mov 	si,cs:tek_mel 	; Адрес текущей ноты
	mov 	bl,cs:[si] 	; Текущая нота
	cmp 	bl,255 	; Пауза ?
	jnz 	muz2
; Выключение звука
	in 	al,61h 	; Чтение состояния системного порта В
	and 	al,0fch 	; Запрещение звучания (биты 0 и 1)
	out 	61h,al	; Запись в системный порт В
	inc 	cs:tek_mel 	; Переход к адресу след. ноты
	ret
muz2: or 	bl,bl 		; = 0 ?
	jnz 	muz3 	; Продолжение
	jmp 	muze 	; Выход, если признак конца
muz3: shl 	bl,1 		; Умножение bl на 2
	xor 	bh,bh 	; bh = 0
	mov 	ax,cs:noty[bx] 	; В DI частота ноты
; Программирование делителя частоты 2 канала
	inc 	cs:tek_mel 	; Переход к адресу след. ноты
	out 	42h,al 	; Мл.байт частоты ? канал 2 таймера
	xchg	 al,ah 	; AH ? AL
	out 	42h,al 	; Ст.байт частоты?? канал 2 таймера
; Разрешение звучания
	in 	al,61h 	; Чтение состояния системного порта В
	or 	al,3 		; Разрешение звучания (биты 0 и 1)
	out 	61h,al 	; Запись в системный порт В
	ret 			; Нормальный выход
muz 	endp
kbin 	proc 	near ; Ввод с клавиатуры и проверка на выбор игры
; Процедура совпадает с одноименной в задаче 2.4.2.1.
kbin 	endp
; Мелодия "Чижик Пыжик"
mel1 	db 	17,17,255,13,13,255,17,17,255,13,13,255,18,18,255
	db 	17,17,255,15,15,15,15,255,255
	db 	8,8,255,8,8,255,8,8,255,10,255,12,255
	db 	13,13,255,13,13,255,13,13,13,13
	db 	0
; Мелодия "Подмосковные вечера"
mel2 	db 	1,1,1,4,4,4,8,8,8,4,4,4,6,6,6,6,6,6,4,4,4,3,3,3
	db 	8,8,8,8,8,8,6,6,6,6,6,6,1,1,1,1,1,1,1,1,1,1,1,1
	db	 0
; Мелодия "Кан-кан"
mel3	db 	18,6,25,13,22,18,25,13,20,1,23,8,22,5,20,1
 	db	 25,6,13,13,25,10,13,13,25,10,27,13,22,10,23,13,20,1
	db 	11,11,20,5,11,11,20,1,23,11,22,5,20,11,18,6,30,18
	db 	29,17,27,15,25,13,23,11,22,10,20,8,18,6,13,13,18,10
	db 	13,13,20,1,23,8,22,5,20,8,25,6,13,13,25,10,13,13
	db 	25,6,27,13,22,10,23,13,20,1,8,8,20,5,8,8,20,1,23,8
	db 	22,5,20,8,18,6,25,10,20,13,22,10,18,6,6,6,6,6,6,6
	db 	34,8,24,12,24,15,34,12,32,1,25,5,25,8,29,5,30,6,34,13
	db 	37,10,34,13,34,1,32,8,32,5,8,8,34,8,24,12,24,15,34,12
	db 	32,1,25,5,25,8,29,5,29,3,27,7,29,10,27,13,34,12,32,8
	db 	34,6,32,3,34,8,24,15,24,12,34,15,32,1,29,8,25,5,29,8
	db 	30,6,34,13,37,10,34,13,34,1,32,5,32,8,5,5,34,8,24,15
	db 	24,12,34,15,32,1,25,6,25,5,29,8,29,3,27,7,29,10,27,7
	db 	32,8,30,6,29,5,27,3,25,1,8,8,25,5,8,8,27,12,30,15
	db 	29,8,27,12,32,1,8,8,32,5,8,8,32,1,34,8,29,5,30,8,27,8
	db 	15,15,27,12,15,15,27,8,30,12,29,15,27,12,25,1,37,1
	db 	36,5,34,6,32,8,30,8,29,10,27,12,25,1,8,8,25,5,8,8
	db 	27,8,30,15,29,12,27,15,32,1,8,8,32,5,8,8,32,1,34,8
	db 	29,5,30,8,27,8,15,15,27,12,15,15,27,8,30,15,29,12
	db 	27,15,25,1,32,8,27,5,29,8,25,1,32,32,37,37,0
; Коэффициенты деления для нот
noty 	dw 	0eeeh,0e18h,0d49h,0c8eh,0bdfh,0b2fh,0abeh
 	dw 9f7h,968h,8e0h,861h,7e8h,777h,70ch,6a5h,647h
 	dw 5edh,597h,547h,4fbh,4b4h,470h,430h,3f4h
 	dw 3bbh,386h,352h,323h,2f6h,2cbh,2a3h,27dh,25ah,238h,218h,1fah
 	dw 1ddh,1c3h,1a9h,192h,17bh,166h,152h,13fh,12dh,11ch,10ch,0fdh
 	dw 0
code 	ends
	END 	Start
